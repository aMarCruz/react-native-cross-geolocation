// Top-level build file where you can add configuration options common to all sub-projects/modules.
import groovy.json.JsonSlurper

// dynamically retrieve version from package.json
def getPackageVersion() {
    return new JsonSlurper().parse(file('../package.json'), 'utf-8').version
}

def safeExtGet(prop, fallback) {
    rootProject.ext.hasProperty(prop) ? rootProject.ext.get(prop) : fallback
}

// Google Play Services 12.0.x to 15.0.x depends on support libraries 26.1.0
def DEF_PLAY_SERVICES_VERSION   = '15.0.1'
def DEF_BUILD_TOOLS_VERSION     = '27.0.3'
def DEF_MIN_SDK_VERSION         = 21
def DEF_COMPILE_SDK_VERSION     = 27
def DEF_TARGET_SDK_VERSION      = 27

buildscript {
    repositories {
        mavenLocal()
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.3'
    }
}

apply plugin: 'com.android.library'

android {
    compileSdkVersion safeExtGet('compileSdkVersion', DEF_COMPILE_SDK_VERSION)
    buildToolsVersion safeExtGet('buildToolsVersion', DEF_BUILD_TOOLS_VERSION)

    defaultConfig {
        minSdkVersion safeExtGet('minSdkVersion', DEF_MIN_SDK_VERSION)
        targetSdkVersion safeExtGet('targetSdkVersion', DEF_TARGET_SDK_VERSION)
        versionCode 1
        versionName computePackageVersion()
    }
    lintOptions {
        abortOnError false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

repositories {
    mavenLocal()
    google()
    jcenter()
    maven {
        url "$rootDir/../node_modules/react-native/android"
        name 'React Native (local)'
    }
}

dependencies {
    def _playServicesVersion  = safeExtGet('googlePlayServicesVersion', DEF_PLAY_SERVICES_VERSION)
    compileOnly 'com.facebook.react:react-native:+'
    implementation "com.google.android.gms:play-services-location:$_playServicesVersion"
}

task customClean(type: Delete) {
    delete rootProject.buildDir
}
clean.dependsOn customClean
