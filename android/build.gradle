// Top-level build file where you can add configuration options common to all sub-projects/modules.
import groovy.json.JsonSlurper

// dynamically retrieve version from package.json
def computePackageVersion() {
    def slurper = new JsonSlurper()
    def json = slurper.parse(file('../package.json'), 'utf-8')
    return json.version
}

/*
    Google Play Services 12.0.x to 15.0.x depends on support libraries 26.1.0
*/
def DEF_PLAY_SERVICES_VERSION   = '15.0.1'
def DEF_BUILD_TOOLS_VERSION     = '27.0.3'
def DEF_MIN_SDK_VERSION         = 21
def DEF_COMPILE_SDK_VERSION     = 27
def DEF_TARGET_SDK_VERSION      = 27

def _playServicesVersion    = rootProject.hasProperty('googlePlayServicesVersion') ? rootProject.googlePlayServicesVersion : DEF_PLAY_SERVICES_VERSION
def _buildToolsVersion      = rootProject.hasProperty('buildToolsVersion') ? rootProject.buildToolsVersion : DEF_BUILD_TOOLS_VERSION
def _minSdkVersion          = rootProject.hasProperty('minSdkVersion') ? rootProject.minSdkVersion : DEF_MIN_SDK_VERSION
def _compileSdkVersion      = rootProject.hasProperty('compileSdkVersion') ? rootProject.compileSdkVersion : DEF_COMPILE_SDK_VERSION
def _targetSdkVersion       = rootProject.hasProperty('targetSdkVersion')  ? rootProject.targetSdkVersion  : DEF_TARGET_SDK_VERSION

buildscript {
    repositories {
        mavenLocal()
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.3'
    }
}

apply plugin: 'com.android.library'

android {
    compileSdkVersion _compileSdkVersion
    buildToolsVersion _buildToolsVersion

    defaultConfig {
        minSdkVersion _minSdkVersion
        targetSdkVersion _targetSdkVersion
        versionCode 1
        versionName computePackageVersion()
    }
    lintOptions {
        abortOnError false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

repositories {
    mavenLocal()
    google()
    jcenter()
    maven { url "$rootDir/../node_modules/react-native/android" }
}

dependencies {
    compileOnly 'com.facebook.react:react-native:+'
    implementation "com.google.android.gms:play-services-location:$_playServicesVersion"
}
